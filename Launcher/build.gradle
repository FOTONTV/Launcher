//file:noinspection GroovyAssignabilityCheck, GroovyAccessibility, GrUnresolvedAccess
apply plugin: 'org.openjfx.javafxplugin'
apply plugin: 'com.github.johnrengelman.shadow'

String mainClassName = "fun.fotontv.launcher.ClientLauncherWrapper"
String mainAgentName = "fun.fotontv.launcher.LauncherAgent"

repositories {
    maven {
        url "https://repo.spring.io/plugins-release/"
    }
}
javafx {
    version = "16"
    modules = ['javafx.controls', 'javafx.fxml']
}

sourceCompatibility = '16'
targetCompatibility = '16'

configurations {
    bundle
    pack
    api.extendsFrom bundle, pack
}

jar {
    //noinspection GrDeprecatedAPIUsage
    classifier = 'clean'
    manifest.attributes("Main-Class": mainClassName,
            "Premain-Class": mainAgentName,
            "Multi-Release": "true")
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

shadowJar {
    duplicatesStrategy = 'EXCLUDE'
    //noinspection GrDeprecatedAPIUsage
    classifier = null
    relocate 'org.objectweb.asm', 'fun.fotontv.repackage.org.objectweb.asm'
    relocate 'io.netty', 'fun.fotontv.repackage.io.netty'
    configurations = [project.configurations.pack]
    exclude 'module-info.class'
}

dependencies {
    pack project(':LauncherAPI')
    bundle group: 'com.github.oshi', name: 'oshi-core', version: rootProject['verOshiCore']
    pack group: 'io.netty', name: 'netty-codec-http', version: rootProject['verNetty']
    pack group: 'org.ow2.asm', name: 'asm-tree', version: rootProject['verAsm']
}

task genRuntimeJS(type: Zip) {
    duplicatesStrategy = 'EXCLUDE'
    archiveFileName = "runtime.zip"
    destinationDirectory = file("${buildDir}/tmp")
    from "runtime/"
}

task dumpLibs(type: Copy) {
    duplicatesStrategy = 'EXCLUDE'
    into "$buildDir/libs/libraries"
    from configurations.bundle
}

assemble.dependsOn tasks.genRuntimeJS, tasks.dumpLibs, tasks.shadowJar

publishing {
    publications {
        launcherclientapi(MavenPublication) {
            artifactId = 'launcher-client-api'
            artifact(jar) {
                classifier ""
            }
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'FLauncher Client API'
                description = 'FLauncher Client Module API'
                url = 'https://launcher.fotontv.fun'
                licenses {
                    license {
                        name = 'GNU General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'fotontv'
                        name = 'FOTONTV'
                        email = 'fotontv@fotontv.fun'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/FOTONTV/Launcher.git'
                    developerConnection = 'scm:git:ssh://git@github.com:FOTONTV/Launcher.git'
                    url = 'https://launcher.fotontv.fun/'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.launcherclientapi
}